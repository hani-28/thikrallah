// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlinVersion = '1.3.72'
        daggerVersion = '2.21'
        androidxVersion = '1.1.0'
        androidxMediaVersion = '1.3.0'
        androidxPreferencesVersion = '1.1.1'
        androidxAppcompatVersion = '1.2.0'
        espressoVersion = '3.3.0'
        okhttpVersion = '3.12.1'


        deps = [
                android: [
                        build: [
                                minSdkVersion    : 21,
                                targetSdkVersion : 29,
                                compileSdkVersion: 30
                        ]
                ],
                kotlin: [
                        stdlib: "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
                ],
                dagger: [
                        apt: "com.google.dagger:dagger-compiler:${daggerVersion}",
                        runtime: "com.google.dagger:dagger:${daggerVersion}"
                ]
        ]
    }

    // workaround for proguard failing to parse moshi's proguard rules
    buildscript {
        configurations.all {
            resolutionStrategy {
                force 'net.sf.proguard:proguard-gradle:6.1.0beta2'
            }
        }
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath 'com.google.gms:google-services:4.3.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.13"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }

    tasks.withType(JavaCompile).all {
        options.compilerArgs += [
                '-Adagger.formatGeneratedSource=disabled',
                '-Adagger.gradle.incremental=enabled'
        ]
    }

    afterEvaluate {
        extensions.findByName('kapt')?.arguments {
            arg("dagger.formatGeneratedSource", "disabled")
            arg("dagger.gradle.incremental", "enabled")
        }
    }
}


